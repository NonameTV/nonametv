#! /usr/bin/perl

use strict;
use warnings;

use File::Compare;
use FindBin;
use lib "$FindBin::Bin/../lib";

use IO::Compress::Gzip qw(gzip $GzipError);
use IO::Uncompress::Gunzip qw(gunzip $GunzipError);
use XML::LibXML;

use NonameTV;
use NonameTV::DataStore;
use NonameTV::Config qw/ReadConfig/;

# Read configuration
my $conf = ReadConfig();

my $ds = NonameTV::DataStore->new( $conf->{DataStore} );

# TODO target, dynamically generate from Xmltv Exporter configuration
my $xmltvdir = $conf->{Exporters}->{Xmltv}->{Root};
my $dvbservicepointerxml = $xmltvdir . '/dvb_service_pointer.xml.gz';
my $iconurl = $conf->{Exporters}->{Xmltv}->{IconRootUrl};

my $doc = XML::LibXML::Document->createDocument( );
my $root = $doc->createElement( 'tv' );
$doc->setDocumentElement( $root );


my $channels = $ds->{sa}->LookupMany( 'channels', { export => 1 },
                                    [ 'xmltvid' ] );

foreach my $channel (@{$channels}) {
  my $serviceNode = $doc->createElement( 'channel' );
  $serviceNode->setAttribute( 'id', $channel->{xmltvid} );
  my $node = $doc->createElement( 'display-name' );
  $node->setAttribute( 'lang', $channel->{sched_lang} );
  $node->appendText( $channel->{display_name} );
  $serviceNode->appendChild( $node );
  $root->appendChild( $serviceNode );

  my $services =  $ds->{sa}->LookupMany( 'dvb_service_pointer', { channel_id => $channel->{id} },
                                       [ 'original_network_id', 'transport_id', 'service_id' ] );

  foreach my $service (@{$services}) {
    my $pointerNode = XML::LibXML::Element->new( 'dvb-service' );
    $node = $doc->createComment( ' ' . $service->{description} . ' ' );
    $pointerNode->appendChild( $node );
    $node = $doc->createElement( 'original_network_id' );
    $node->appendText( $service->{original_network_id} );
    $pointerNode->appendChild( $node );
    if( $service->{transport_id} ){
      $node = $doc->createElement( 'transport_id' );
      $node->appendText( $service->{transport_id} );
      $pointerNode->appendChild( $node );
    }
    $node = $doc->createElement( 'service_id' );
    $node->appendText( $service->{service_id} );
    $pointerNode->appendChild( $node );
    $serviceNode->appendChild( $pointerNode );
  }
}

my $xml = $doc->toString( 1 );
gzip \$xml => $dvbservicepointerxml . '.new', Level => 9, Time => 0
        or die "gzip failed: $GzipError\n";

if( compare( $dvbservicepointerxml, $dvbservicepointerxml . '.new' )== 0 ){
  unlink( $dvbservicepointerxml . '.new' );
} else {
  rename( $dvbservicepointerxml . '.new', $dvbservicepointerxml );
}
