#!/bin/perl -w

use strict;

use NonameTV;
use NonameTV::DataStore;

use Getopt::Long;
use Text::CSV_XS;

my $opt = { };

my $res = GetOptions( $opt, qw// );

if( scalar( @ARGV ) != 2 )
{
  print << 'EOHELP';
nonametv-dumpdb <data-directory> <country>

Dump the contents of the database in a format suitable for inclusion
in a revision-control system.

Note that only tables that contain configuration is included. Tables
that contain data generated by the Importers are not included.

The following tables are included:

channels
trans_cat
languagestrings

EOHELP

  exit 1;
}

my( $datadir, $country ) = @ARGV;

# Read configuration
my $conf = NonameTV::ReadConfig();

my $ds = NonameTV::DataStore->new( $conf->{DataStore} );

mysqldump( "--add-drop-table --no-data batches channels files programs state trans_cat languagestrings", "$datadir/listings.sql" );

dump_table( $ds, "languagestrings", "$datadir/languagestrings.txt" );
dump_table( $ds, "channels", "$datadir/$country/channels.txt" );
dump_table( $ds, "trans_cat", "$datadir/$country/trans_cat.txt" );

sub mysqldump {
  my( $command, $outfile ) = @_;

  my $dbuser = $conf->{DataStore}->{username};
  my $dbpassword = $conf->{DataStore}->{password};
  my $dbname = $conf->{DataStore}->{dbname};

  print "Writing database structure to $outfile\n";

  my $pw="";
  if( length( $dbpassword ) > 0 ) {
    $pw = "-p$dbpassword";
  }
  system( "mysqldump -u$dbuser $pw $dbname $command > $outfile" );
}

sub dump_table {
  my( $ds, $table, $file ) = @_;

  print "Dumping table $table to file $file\n";

  my $out = new IO::File( "> $file" ) 
      or die "Failed to open $file for writing";

  my $csv = Text::CSV_XS->new({eol=> "\n"});

  my $sth = $ds->Iterate( $table, {} );

  while( my $row = $sth->fetchrow_arrayref ) {
    foreach (@{$row}) {
      $_='\N' if not defined( $_ );
    }

    $csv->print( $out, $row );
  }

  $sth->finish();

  $out->close();
}
