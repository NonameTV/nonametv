#!/usr/bin/perl -w

# Update the channels-table with information about which channels
# have an icon.

use strict;

use FindBin;
use lib "$FindBin::Bin/../lib";

use NonameTV;
use NonameTV::DataStore;
use NonameTV::Config qw/ReadConfig/;
use NonameTV::Factory qw/CreateDataStore/;

use LWP::Simple;
use Getopt::Long;

my $opt = { 
  verbose => 0,
  quiet => 0,
  unattended => 0,
};

my $res = GetOptions( $opt, qw/verbose quiet unattended/ );

# Read configuration
my $conf = ReadConfig();

# Create Datastore
my $ds = CreateDataStore();

my $iconroot = $conf->{Exporters}->{Xmltv}->{IconRootUrl};

my $sth = $ds->sa->Iterate( 'channels' );

while( my $entry = $sth->fetchrow_hashref() ) {
  my $xmltvid = $entry->{xmltvid};
  my $logo = $entry->{logo};

  print "Processing $xmltvid\n" if $opt->{verbose};

  my $url = "$iconroot$xmltvid.png";
  if( head( $url ) ) {
    if( not $logo ) {
      $ds->sa->Update( 'channels', { xmltvid => $xmltvid }, { logo => 1 } );
      print "Added icon for $xmltvid\n" if not $opt->{quiet};
    }
  }
  else
  {
    if( $logo ) {
      if( $opt->{unattended} ) {
        print "Icon for $xmltvid has disappeared.\n";
      }
      else {
        $ds->sa->Update( 'channels', { xmltvid => $xmltvid }, { logo => 0 } );
        print "Removed icon for $xmltvid\n" if not $opt->{quiet};
      }
    }
  }
}

$sth->finish();
